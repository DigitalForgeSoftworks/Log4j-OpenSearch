plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

group 'org.digitalforge.log4j'
version '1.0.4'

repositories {
    mavenCentral()
}

dependencies {

    implementation 'com.conversantmedia:disruptor:1.2.21'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.17.2'
    implementation 'io.micrometer:micrometer-core:1.13.3'
    implementation 'org.apache.logging.log4j:log4j-core:2.23.1'
    implementation 'org.digitalforge:sneakythrow:1.0.1'
    implementation 'org.opensearch.client:opensearch-java:2.13.0'
    implementation 'org.opensearch.client:opensearch-rest-client:2.13.0'

    annotationProcessor 'org.apache.logging.log4j:log4j-core:2.23.1'

}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(11)
    }
    withJavadocJar()
    withSourcesJar()
}

javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

publishing {
    repositories {
        maven {
            url = 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'
            credentials {
                username = sonatypeUsername
                password = sonatypePassword
            }
        }
    }
    publications {
        mavenJava(MavenPublication) {
            from components.java
            pom {

                name = 'Log4j-OpenSearch'
                description = 'OpenSearch Appender for Log4j'
                url = 'https://github.com/DigitalForgeSoftworks/log4j-opensearch/'

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id = 'zreed'
                        name = 'Zachary Reed'
                        email = 'zach@digitalforge.org'
                    }
                }

                scm {
                    connection = 'scm:git:https://github.com/DigitalForgeSoftworks/log4j-opensearch.git'
                    developerConnection = 'scm:git:git@github.com/DigitalForgeSoftworks/log4j-opensearch.git'
                    url = 'https://github.com/DigitalForgeSoftworks/log4j-opensearch/'
                }

            }
        }
    }
}

signing {
    useGpgCmd()
    sign publishing.publications.mavenJava
}
